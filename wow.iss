; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "World of Warcraft"
#define MyAppLongName "World of Warcraft: Wrath of the Lich King"
#define MyAppVersion "3.3.5a (12340)"
#define MyAppPublisher "Ivailo Monev"
#define MyAppExeName "Wow.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{7D3FD814-6512-49CE-858D-5E7339D13B65}
AppName={#MyAppLongName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={commonpf32}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
; Remove the following line to run in administrative install mode (install for all users.)
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=commandline
OutputDir="installer"
OutputBaseFilename=WoW_3.3.5a
SetupIconFile="wow.ico"
InfoBeforeFile=wow.rtf
WizardImageFile=wow.bmp
WizardSmallImageFile=wow-small.bmp
WizardImageAlphaFormat=defined
Compression=none
SolidCompression=no
WizardStyle=modern
DiskSpanning=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "armenian"; MessagesFile: "compiler:Languages\Armenian.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"
Name: "catalan"; MessagesFile: "compiler:Languages\Catalan.isl"
Name: "corsican"; MessagesFile: "compiler:Languages\Corsican.isl"
Name: "czech"; MessagesFile: "compiler:Languages\Czech.isl"
Name: "danish"; MessagesFile: "compiler:Languages\Danish.isl"
Name: "dutch"; MessagesFile: "compiler:Languages\Dutch.isl"
Name: "finnish"; MessagesFile: "compiler:Languages\Finnish.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "hebrew"; MessagesFile: "compiler:Languages\Hebrew.isl"
Name: "icelandic"; MessagesFile: "compiler:Languages\Icelandic.isl"
Name: "italian"; MessagesFile: "compiler:Languages\Italian.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "norwegian"; MessagesFile: "compiler:Languages\Norwegian.isl"
Name: "polish"; MessagesFile: "compiler:Languages\Polish.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "slovak"; MessagesFile: "compiler:Languages\Slovak.isl"
Name: "slovenian"; MessagesFile: "compiler:Languages\Slovenian.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"
Name: "ukrainian"; MessagesFile: "compiler:Languages\Ukrainian.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Types]
Name: full; Description: "Default"
Name: custom; Description: "Custom"; Flags: iscustom

[Components]
; Game data files
Name: "Main"; Description: "Main game files"; Types: full custom; Flags: fixed
Name: "Sound"; Description: "Music, ambiance and sound effects"; Types: full
Name: "Cinematic"; Description: "Cinematics"; Types: full
; CVars, check https://wowwiki.fandom.com/wiki/Console_variables
Name: "gxAPI"; Description: "Graphics API to use:"; Types: full custom; Flags: fixed
Name: "gxAPI\auto"; Description: "Automatic (default)"; Types: full; Flags: exclusive
Name: "gxAPI\d3d9"; Description: "DirectX 9.0"; Types: custom; Flags: exclusive
Name: "gxAPI\opengl"; Description: "OpenGL"; Types: custom; Flags: exclusive
Name: "maxFPS"; Description: "Limit FPS to:"; Types: full custom; Flags: fixed
Name: "maxFPS\200"; Description: "200 (default)"; Types: full; Flags: exclusive
Name: "maxFPS\100"; Description: "100"; Types: custom; Flags: exclusive
Name: "maxFPS\60"; Description: "60"; Types: custom; Flags: exclusive
Name: "maxFPS\30"; Description: "30"; Types: custom; Flags: exclusive
Name: "textureCacheSize"; Description: "Texture cache size:"; Types: full custom; Flags: fixed
Name: "textureCacheSize\32"; Description: "32 (default)"; Types: full; Flags: exclusive
Name: "textureCacheSize\64"; Description: "64 (requires system with 1GB RAM or more)"; Types: custom; Flags: exclusive 

[Files]
Source: "{src}\optional-sound.bin"; DestDir: "{app}\Data"; DestName: "patch-2.MPQ"; Flags: ignoreversion external; Components: Sound
Source: "{src}\optional-sound-2.bin"; DestDir: "{app}\Data\enGB"; DestName: "patch-enGB-2.MPQ"; Flags: ignoreversion external; Components: Sound
Source: "{src}\optional-cinematic.bin"; DestDir: "{app}\Data"; DestName: "patch-3.MPQ"; Flags: ignoreversion external; Components: Cinematic
Source: "D:\Games\World of Warcraft 3.3.5\dbghelp.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\DivxDecoder.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\WoW.mfil"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Wow.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\WowError.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\common.MPQ"; DestDir: "{app}\Data"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\expansion.MPQ"; DestDir: "{app}\Data"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\lichking.MPQ"; DestDir: "{app}\Data"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\patch.MPQ"; DestDir: "{app}\Data"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\AccountBilling.url"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\connection-help.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\Credits.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\Credits_BC.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\Credits_LK.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\eula.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\expansion-locale-enGB.MPQ"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\lichking-locale-enGB.MPQ"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\locale-enGB.MPQ"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\patch-enGB.MPQ"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\speech-enGB.MPQ"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\TechSupport.url"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\termination.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\Data\enGB\tos.html"; DestDir: "{app}\Data\enGB"; Flags: ignoreversion
Source: "D:\Games\World of Warcraft 3.3.5\WTF\Config.wtf"; DestDir: "{app}\WTF"; Flags: ignoreversion; AfterInstall: PostInstallConfigSetup
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[Code]
var
  RealmListPage: TInputQueryWizardPage;

procedure InitializeWizard;
begin
  RealmListPage := CreateInputQueryPage(wpSelectComponents,
    'Realm', 'Please specify the realmlist value of your prefered server.', '');
  RealmListPage.Add('&Realm:', False);
  RealmListPage.Values[0] := 'change me';
end;

procedure PostInstallConfigSetup();
begin
  if WizardIsComponentSelected('Sound') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableAllSound "1"', True);
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableMusic "1"', True);
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableAmbience "1"', True);
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableSFX "1"', True);
  end else begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableAllSound "0"', True);
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableMusic "0"', True);
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableAmbience "0"', True);
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET Sound_EnableSFX "0"', True);
  end;

  if WizardIsComponentSelected('Cinematic') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET movie "1"', True);
  end else begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET movie "0"', True);
  end;

  if WizardIsComponentSelected('gxAPI\d3d9') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET gxAPI "d3d9"', True);
  end else if WizardIsComponentSelected('gxAPI\opengl') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET gxAPI "opengl"', True);
  end else begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET gxAPI "auto"', True);
  end;

  if WizardIsComponentSelected('maxFPS\100') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET maxFPS "100"', True);
  end else if WizardIsComponentSelected('maxFPS\60') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET maxFPS "60"', True);
  end else if WizardIsComponentSelected('maxFPS\30') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET maxFPS "30"', True);
  end else begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET maxFPS "200"', True);
  end;

  if WizardIsComponentSelected('textureCacheSize\64') then begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET textureCacheSize "64"', True);
  end else begin
    SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET textureCacheSize "32"', True);
  end;

  SaveStringToFile(ExpandConstant(CurrentFileName), #13#10 + 'SET realmList "' + RealmListPage.Values[0] + '"', True);
end;